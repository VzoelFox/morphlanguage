# core/translator_shard/statement_visitor.fox
# Visitor untuk pernyataan

ambil_semua "../morph_t.fox" sebagai T
ambil_semua "../absolute_syntax_morph.fox" sebagai AST
ambil_semua "runtime.fox" sebagai RT
ambil_semua "expression_visitor.fox"
ambil_sebagian tambah, panjang dari "../cotc(stdlib)/core.fox"

kelas StatementVisitor warisi ExpressionVisitor maka

    asink fungsi kunjungi_Bagian(node) maka
        biar i = 0
        selama i < panjang(node.daftar_pernyataan) maka
            tunggu ini._eksekusi(node.daftar_pernyataan[i])
            ubah i = i + 1
        akhir
    akhir

    asink fungsi kunjungi_PernyataanEkspresi(node) maka
        tunggu ini._evaluasi(node.ekspresi)
    akhir

    asink fungsi kunjungi_CobaTangkap(node) maka
        coba
            tunggu ini._eksekusi_blok(node.blok_coba, RT.Lingkungan(ini.lingkungan))
        tangkap e
            jika jenis(e) == RT.NilaiKembalian atau jenis(e) == RT.BerhentiLoop atau jenis(e) == RT.LanjutkanLoop maka
                lemparkan e
            akhir

            biar lingkungan_tangkap = RT.Lingkungan(ini.lingkungan)

            biar ditangkap = salah
            biar i = 0
            selama i < panjang(node.daftar_tangkap) maka
                biar tangkap_node = node.daftar_tangkap[i]

                jika tangkap_node.nama_error != nil maka
                    lingkungan_tangkap.definisi(tangkap_node.nama_error.nilai, e, salah)
                akhir

                biar guard_ok = benar
                jika tangkap_node.kondisi_jaga != nil maka
                    biar eval_guard = tunggu ini._evaluasi_dalam_lingkungan(tangkap_node.kondisi_jaga, lingkungan_tangkap)
                    ubah guard_ok = ini._apakah_benar(eval_guard)
                akhir

                jika guard_ok maka
                    tunggu ini._eksekusi_blok(tangkap_node.badan, lingkungan_tangkap)
                    ubah ditangkap = benar
                    berhenti
                akhir

                ubah i = i + 1
            akhir

            jika tidak ditangkap maka
                lemparkan e
            akhir
        akhir

        jika node.blok_akhirnya != nil maka
             tunggu ini._eksekusi_blok(node.blok_akhirnya, RT.Lingkungan(ini.lingkungan))
        akhir
    akhir

    asink fungsi kunjungi_DeklarasiVariabel(node) maka
        biar nilai = nil
        jika node.nilai != nil maka
            ubah nilai = tunggu ini._evaluasi(node.nilai)
        akhir
        biar adalah_konstan = (node.jenis_deklarasi.tipe == T.TETAP)
        ini.lingkungan.definisi(node.nama.nilai, nilai, adalah_konstan)
    akhir

    asink fungsi kunjungi_Tulis(node) maka
        biar output = ""
        biar i = 0
        selama i < panjang(node.argumen) maka
            biar val = tunggu ini._evaluasi(node.argumen[i])
            ubah output = output + ini._ke_string(val)
            jika i < panjang(node.argumen) - 1 maka
                ubah output = output + " "
            akhir
            ubah i = i + 1
        akhir
        tulis(output)
    akhir

    asink fungsi kunjungi_Assignment(node) maka
        biar nilai = tunggu ini._evaluasi(node.nilai)
        biar target = node.target

        jika jenis(target) == AST.Identitas maka
            ini.lingkungan.tetapkan(target.token, nilai)
        lain
            jika jenis(target) == AST.Akses maka
                biar objek = tunggu ini._evaluasi(target.objek)
                biar kunci = tunggu ini._evaluasi(target.kunci)

                ubah objek[kunci] = nilai
            lain
                jika jenis(target) == AST.AmbilProperti maka
                    biar objek = tunggu ini._evaluasi(target.objek)
                    jika jenis(objek) != RT.MorphInstance maka
                         lemparkan RT.KesalahanTipe(target.nama, "Hanya properti dari instance kelas yang bisa diubah.")
                    akhir
                    biar adalah_akses_internal = salah
                    objek.tetapkan(target.nama, nilai, adalah_akses_internal)
                lain
                    lemparkan RT.KesalahanRuntime(node.target, "Target assignment tidak valid.")
                akhir
            akhir
        akhir
    akhir

    asink fungsi kunjungi_PernyataanKembalikan(node) maka
        biar nilai = nil
        jika node.nilai != nil maka
            ubah nilai = tunggu ini._evaluasi(node.nilai)
        akhir
        lemparkan RT.NilaiKembalian(nilai)
    akhir

    asink fungsi kunjungi_Berhenti(node) maka
        lemparkan RT.BerhentiLoop()
    akhir

    asink fungsi kunjungi_Lanjutkan(node) maka
        lemparkan RT.LanjutkanLoop()
    akhir

    asink fungsi kunjungi_Selama(node) maka
        biar counter = 0
        biar batas = 10000

        selama ini._apakah_benar(tunggu ini._evaluasi(node.kondisi)) maka
            ubah counter = counter + 1
            jika counter > batas maka
                lemparkan RT.KesalahanRuntime(node.token, "Loop melebihi batas iterasi maksimum.")
            akhir

            coba
                tunggu ini._eksekusi_blok(node.badan, RT.Lingkungan(ini.lingkungan))
            tangkap e
                jika jenis(e) == RT.LanjutkanLoop maka
                    lanjutkan
                lain
                    jika jenis(e) == RT.BerhentiLoop maka
                        berhenti
                    lain
                        lemparkan e
                    akhir
                akhir
            akhir
        akhir
    akhir

    asink fungsi kunjungi_JikaMaka(node) maka
        biar kond = tunggu ini._evaluasi(node.kondisi)
        jika ini._apakah_benar(kond) maka
            tunggu ini._eksekusi_blok(node.blok_maka, RT.Lingkungan(ini.lingkungan))
        lain
            biar handled = salah
            biar i = 0
            selama i < panjang(node.rantai_lain_jika) maka
                biar pair = node.rantai_lain_jika[i]
                biar k = tunggu ini._evaluasi(pair[0])
                jika ini._apakah_benar(k) maka
                    tunggu ini._eksekusi_blok(pair[1], RT.Lingkungan(ini.lingkungan))
                    ubah handled = benar
                    berhenti
                akhir
                ubah i = i + 1
            akhir

            jika tidak handled dan node.blok_lain != nil maka
                tunggu ini._eksekusi_blok(node.blok_lain, RT.Lingkungan(ini.lingkungan))
            akhir
        akhir
    akhir

    asink fungsi kunjungi_Pilih(node) maka
        biar nilai_ekspresi = tunggu ini._evaluasi(node.ekspresi)
        biar kasus_cocok = salah

        biar i = 0
        selama i < panjang(node.kasus) maka
            biar kasus = node.kasus[i]
            biar j = 0
            selama j < panjang(kasus.nilai) maka
                biar val = tunggu ini._evaluasi(kasus.nilai[j])
                jika nilai_ekspresi == val maka
                    tunggu ini._eksekusi_blok(kasus.badan, RT.Lingkungan(ini.lingkungan))
                    ubah kasus_cocok = benar
                    berhenti
                akhir
                ubah j = j + 1
            akhir

            jika kasus_cocok maka
                berhenti
            akhir
            ubah i = i + 1
        akhir

        jika tidak kasus_cocok dan node.kasus_lainnya != nil maka
            tunggu ini._eksekusi_blok(node.kasus_lainnya.badan, RT.Lingkungan(ini.lingkungan))
        akhir
    akhir

    asink fungsi kunjungi_Jodohkan(node) maka
        biar nilai_ekspresi = tunggu ini._evaluasi(node.ekspresi)

        biar i = 0
        selama i < panjang(node.kasus) maka
            biar kasus = node.kasus[i]
            biar lingkungan_kasus = RT.Lingkungan(ini.lingkungan)

            biar cocok_result = tunggu ini._pola_cocok(kasus.pola, nilai_ekspresi, lingkungan_kasus)
            biar cocok = cocok_result[0]

            jika cocok maka
                jika kasus.jaga != nil maka
                    biar nilai_jaga = tunggu ini._evaluasi_dalam_lingkungan(kasus.jaga, lingkungan_kasus)
                    jika tidak ini._apakah_benar(nilai_jaga) maka
                        lanjutkan
                    akhir
                akhir

                tunggu ini._eksekusi_blok(kasus.badan, lingkungan_kasus)
                kembali
            akhir

            ubah i = i + 1
        akhir
    akhir

akhir
